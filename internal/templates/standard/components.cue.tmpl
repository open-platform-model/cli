// Components defines the workloads for this module.
// Separated from module.cue for better maintainability.
package main

import (
	resources_workload "opmodel.dev/resources/workload@v0"
	traits_workload "opmodel.dev/traits/workload@v0"
	traits_network "opmodel.dev/traits/network@v0"
)

// #components contains component definitions using resources and traits.
#components: {
	// Web frontend component
	web: {
		resources_workload.#Container
		traits_workload.#Replicas
		traits_network.#Expose

		spec: {
			container: {
				image: #values.web.image
				ports: http: {
					targetPort: 80
				}
				env: apiUrl: {
					name:  "API_URL"
					value: "http://api:\(#values.api.port)"
				}
			}
			replicas: #values.web.replicas
			expose: {
				ports: http: container.ports.http & {
					exposedPort: #values.web.port
				}
				type: "ClusterIP"
			}
		}
	}

	// API backend component
	api: {
		resources_workload.#Container
		traits_workload.#Replicas

		spec: {
			container: {
				image: #values.api.image
				ports: http: {
					targetPort: #values.api.port
				}
			}
			replicas: #values.api.replicas
		}
	}
}
